apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

configurations {
    javadocDeps
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // 17.0.0 exists, but it brings in AndroidX which breaks everything unless we migrate as well.
    implementation 'com.google.android.gms:play-services-ads-identifier:16.0.0'
    javadocDeps 'com.google.android.gms:play-services-ads-identifier:16.0.0'

    // Annotations are included in play-services-ads, but we want to explicitly depend
    // on latest version so it does not clash with the androidTest version
    implementation 'com.android.support:support-annotations:28.0.0'

    // Newer versions exist, but they are API21+ only.
    implementation 'com.squareup.okhttp3:okhttp:3.12.6'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'org.mockito:mockito-android:2.28.2'
}


//------------- Jar Generation code. ---------------//
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps

    exclude '**/R.html', '**/R.*.html', '**/index.html'

    options {
        links "http://docs.oracle.com/javase/8/docs/api/"
        linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"

        tags = [ "apiNote", "implSpec", "implNote" ]
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task buildBranchSearchArchives(type: Copy) {
    dependsOn 'assembleRelease'
    dependsOn 'javadocJar'
    into('../Archives')

    from('build/intermediates/intermediate-jars/release/') {
        include('classes.jar')
        rename ('classes.jar', 'BranchSearchSDK_v'+project.VERSION_NAME+'.jar')
    }

    from('build/libs/') {
        include('BranchSearchSDK-javadoc.jar')
        rename ('BranchSearchSDK-javadoc.jar', 'BranchSearchSDK-javadoc_v'+project.VERSION_NAME+'.jar')
    }
}

apply from: 'https://raw.githubusercontent.com/BranchMetrics/GradleToMavenOptionalDependencies/master/gradle-maven-push.gradle'
